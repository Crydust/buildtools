apply plugin: 'java'
apply plugin: 'application'

group = 'be.crydust.greeter'
version = '1.0-SNAPSHOT'
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'be.crydust.greeter.App'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.6'
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    //runtime 'ch.qos.logback:logback-core:1.1.2'

    testCompile 'junit:junit:4.11'
    //testCompile 'org.hamcrest:hamcrest-core:1.3'
}

jar {
    manifest.attributes(
        'Main-Class': project.mainClassName,
        'Class-Path': configurations.runtime.files.collect{"lib/${it.name}"}.join(' ')
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task distribution(type: Zip) {
    classifier = 'distribution'
    into("${project.name}-${project.version}") {
        from("$projectDir") {
            include 'README.txt'
            include 'NOTICE.txt'
            include 'LICENSE.txt'
        }
        from jar
        from sourcesJar
        from javadocJar
        into('lib') {
            from configurations.runtime
        }
    }
}

